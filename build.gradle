plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "com.google.protobuf" version "0.9.1"
    id "io.freefair.lombok" version "6.5.1"
    id "com.netflix.dgs.codegen" version '5.3.0'
    id 'idea'
    id 'java'

}

apply from: "$rootDir/gradle/testing.gradle"
apply from: "$rootDir/gradle/docker.gradle"
apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/gradle/spring-properties.gradle"

group = 'de.dreipc.xcurator'
sourceCompatibility = '17'

//// Fix for 5.4.0 due to dependency resolution: https://github.com/Netflix/dgs-framework/issues/1281
ext['graphql-java.version'] = '19.2'

repositories {
    maven {
        name "3pc Maven Repository"
        url "https://nexus.3pc.de/repository/maven-group/"
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.2'
    }
}


generateJava {
    // packageName and subPackageNameTypes are set to generate correct union | interfaces
    packageName = "dreipc.graphql"
    generateClient = false
    typeMapping = [

            "MuseumObject"     : "de.dreipc.xcurator.xcuratorimportservice.models.MuseumObject",
            "NamedEntity"      : "de.dreipc.xcurator.xcuratorimportservice.namedentities.NamedEntity",
            "TextContent"      : "de.dreipc.xcurator.xcuratorimportservice.models.TextContent",
            "MuseumObjectTopic": "de.dreipc.xcurator.xcuratorimportservice.topics.MuseumObjectTopic",
            "Node"             : "de.dreipc.xcurator.xcuratorimportservice.models.Node",

            // Common
            "URL"              : "java.net.URL",
            "HTML"             : "java.lang.String",
            "CountConnection"  : "dreipc.common.graphql.relay.CountConnection",
    ]

}


dependencies {
    testImplementation 'org.testng:testng:7.1.0'
    annotationProcessor 'org.springframework:spring-context-indexer'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation "com.deepl.api:deepl-java:0.2.1"

    // Data
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'io.mongock:mongock:5.1.6'
    implementation 'io.mongock:mongock-springboot:5.1.6'
    implementation 'io.mongock:mongodb-springdata-v3-driver:5.1.6'

    // version 0.10.2 is buggy which reflects in migration is not working!
    implementation('org.reflections:reflections') {
        version {
            strictly '0.10.1'
        }
    }

    implementation("io.github.coordinates2country:coordinates2country:1.2")

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.11.1'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'de.dreipc:graphql:5.4.1-dev.239'
    implementation 'de.dreipc:rabbitmq-proto:0.2.0-dev.69'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
